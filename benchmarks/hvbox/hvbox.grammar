traversal pre {
    Root { visit; recur root }
    HBox { loop childs { visit }; loop childs { recur } }
    VBox { loop childs { visit }; loop childs { recur } }
    Leaf { visit }
}

traversal post {
    Root { recur root; visit }
    HBox { loop childs { recur }; loop childs { visit }; visit }
    VBox { loop childs { recur }; loop childs { visit }; visit }
    Leaf { visit }
}

interface Top { }

interface HVBox {
    output width : int;
    output height : int;
    output width_out : int; // outer width
    output height_out : int; // outer height
    output right : int;
    output bottom : int;
    input visible : bool;
}

class Root : Top {
    children {
        root : HVBox;
    }
    evaluation {
        root.right := root.width;
        root.bottom := root.height;
        root.width_out := root.width;
        root.height_out := root.height;
    }
}

class HBox : HVBox {
    children {
        childs : [HVBox];
    }
    evaluation {
        loop childs {
            childs.width_out := fold 0 .. childs$-.width_out + childs$i.width;
            childs.height_out := fold 0 .. childs$-.height_out > childs$i.height ? childs$-.height_out : childs$i.height;
            childs.right := fold (self.right - self.width) .. childs$-.right + childs$i.width;
            childs.bottom := self.bottom;
        }

        width := visible ? childs$$.width_out : 0;
        height := visible ? childs$$.height_out : 0;
    }
}

class VBox : HVBox {
    children {
        childs : [HVBox];
    }
    evaluation {
        loop childs {
            childs.width_out := fold 0 .. childs$-.width_out > childs$i.width ? childs$-.width_out : childs$i.width;
            childs.height_out := fold 0 .. childs$-.height_out + childs$i.height;
            childs.bottom := fold self.bottom .. childs$-.bottom + childs$i.height;
            childs.right := self.right;
        }

        width := visible ? childs$$.width_out : 0;
        height := visible ? childs$$.height_out : 0;
    }
}

class Leaf : HVBox {
    attributes {
        input width_in : int;
        input height_in : int;
    }
    evaluation {
        width := width_in;
        height := height_in;
    }
}
